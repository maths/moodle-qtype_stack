/*  Author Chris Sangwin
    University of Edinburgh
    Copyright (C) 2023 Chris Sangwin

    This program is free software: you can redistribute it or modify
    it under the terms of the GNU General Public License version two.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
    GNU General Public License for details.

    You should have received a copy of the GNU General Public License
    along with this program. If not, see <http://www.gnu.org/licenses/>. */

/* *************************************************************** */
/* Function associate with dealing with proof                      */
/* These need to be in the STACK core, e.g. error trapping etc     */
/* *************************************************************** */


/******************************************************************/
/* Types of proof.                                                */
/*                                                                */
/* proof()       - general, unspecified proof                     */
/* proof_c()     - general proof, with commutative arguments      */
/* proof_opt()   - proof_opt()                                    */
/*                                                                */
/* proof_iff()   - if any only if                                 */
/* proof_cases() - proof by exhaustive cases, the first element   */
/*                 is assumed to justify the cases.               */
/* proof_goal()  - proof by establishing a goal, the last element */
/*                 is assumed to justify by the previous cases.   */
/* proof_ind()   - proof by induction                             */
/*                                                                */
/******************************************************************/

/* General proof functions                                        */
/* Please update Proof/Proof_CAS_library.md and                   */
/* stack/maxima/contrib/prooflib.mac with new types.              */

/* Note, "proof" is assumed to come first in this list, as we use "rest" below for other types. */
proof_types:[proof, proof_c, proof_opt, proof_iff, proof_cases, proof_goal, proof_ind];

proofp(ex) := block(
  if atom(ex) then true,
  if elementp(op(ex), setify(proof_types)) then return(true),
  return(false)
);

s_test_case(proofp(proof(1,2,3)), true);
s_test_case(proofp(proof_iff(1,2)), true);
s_test_case(proofp(sin(x)), false);

proof_validatep(ex) := block(
  if atom(ex) then return(true),
  if op(ex) = proof_opt then
      if not(is(length(args(ex)) = 1)) then return(false)
                                       else return(all_listp(proof_validatep, args(ex))),
  if op(ex) = proof_iff then
      if not(is(length(args(ex)) = 2)) then return(false)
                                       else return(all_listp(proof_validatep, args(ex))),
  if op(ex) = proof_ind then
      if not(is(length(args(ex)) = 4)) then return(false)
                                       else return(all_listp(proof_validatep, args(ex))),
  /* Validate all remaning proof types. */
  if proofp(ex) then return(all_listp(proof_validatep, args(ex))),
  return(false)
);

s_test_case(proof_validatep(proof(1,2,3)), true);
s_test_case(proof_validatep(proof(1,2,proof(4,5,6))), true);
s_test_case(proof_validatep(proof(1,2,proof_iff(4,5))), true);
/* proof_opt must have exactly one sub-proof. */
s_test_case(proof_validatep(proof(1,2,proof_opt(4,5))), false);
/* proof_iff must have exactly two sub-proofs. */
s_test_case(proof_validatep(proof(1,2,proof_iff(4))), false);
s_test_case(proof_validatep(proof(1,2,proof_iff(4,5,6))), false);
/* proof_ind must have exactly four sub-proofs. */
s_test_case(proof_validatep(proof_ind(1,proof(2,3),proof(4,5),6)), true);
s_test_case(proof_validatep(proof_ind(1,proof(2,3),proof(4,5))), false);
s_test_case(proof_validatep(proof(1,proof_opt(2),proof_iff(4,5))), true);


/* Is this a type of proof which can reorder its arguments? */
proof_commutep(ex):=block(
    if atom(ex) then false,
    if is(op(ex)=proof_c) then return(true),
    if is(op(ex)=proof_iff) then return(true),
    return(false)
);

/* Takes a proof tree and flattens this to a list. */
proof_flatten(ex) := apply(proof, flatten(ev(ex, map(lambda([ex2], ex2="["), proof_types))));

s_test_case(proof_flatten(proof_iff(proof(A,B),proof(C))), proof(A,B,C));
s_test_case(proof_flatten(proof_c(proof(A,proof(B,C)),proof(D))), proof(A,B,C,D));

/*
 * Create a normalised proof tree.
 * To establish equivalence of proof trees we compare the normalised form.
 * This basically sorts and "simplifies" its arguments.
 * We also remove the proof_opt tag.
*/
proof_normal(ex) := block(
  if atom(ex) then return(ex),
  if op(ex) = proof_opt then return(first(args(ex))),
  /* Only sort arguments to types of proof which commute. */
  if proof_commutep(ex) then return(apply(op(ex), sort(map(proof_normal, args(ex))))),
  /* Some proof types have subsets of arguments which commute. */
  if op(ex) = proof_cases then return(apply(proof_cases, append([first(args(ex))], sort(map(proof_normal, rest(args(ex))))))),
  if op(ex) = proof_goal then return(apply(proof_goal, append(sort(map(proof_normal, reverse(rest(reverse(args(ex)))))), [first(reverse(args(ex)))]))),
  if op(ex) = proof_ind then return(apply(proof_ind,append([first(args(ex))], sort([proof_normal(second(args(ex))), proof_normal(third(args(ex)))]), [fourth(args(ex))]))),
  return(apply(op(ex), map(proof_normal, args(ex))))
);

s_test_case(proof_normal(proof_c(B,A,D,C)), proof_c(A,B,C,D));
s_test_case(proof_normal(proof_iff(B,A)), proof_iff(A,B));
s_test_case(proof_normal(proof_ind(D,C,B,A)), proof_ind(D,B,C,A));
s_test_case(proof_normal(proof_cases(D,C,B,A)), proof_cases(D,A,B,C));
s_test_case(proof_normal(proof_goal(D,C,B,A)), proof_goal(B,C,D,A));
s_test_case(proof_normal(proof_iff(proof_c(proof_opt(C),A), B)), proof_iff(proof_c(A,C),B));


/******************************************************************/
/*                                                                */
/*  STACK Parson's block functions                                */
/*                                                                */
/******************************************************************/

/*
 * Hashes the keys of a two-dimensional steps array using Base64 hashing. We use Base64 due to its
 * reversibility.
 */
hash_keys_array(steps) := block(
  return(map(lambda([item], join([base64(first(item))], rest(item, 1))), steps))
);

/*
 * Turns a list of Base64-hashed keys into a list containing their original strings.
 */
unhash_keys_list(hashed_list) := block(
  return(map(lambda([item], base64_decode(item)), hashed_list))
);

/*
 * Takes a two-dimensional steps array and returns a JSON string mapping original keys to their Base64 encodings.
 */
parsons_hash_map(steps) := block(
  return(stackjson_stringify(cons("stack_map", makelist([steps[i][1], base64(steps[i][1])], i, 1, length(steps)))))
);

/*
 * Turns a two-dimensional proof steps array into a JSON string with Base64-hashed keys.
 */
parsons_encode(steps) := block(
  return(stackjson_stringify(hash_keys_array(steps)))
);

/*
 * Take the hashed JSON from STACK Parson's block and return a proof function.
 */
parsons_decode(st) := block([pf],
  pf:first(first(stackjson_parse(st))),
  pf:ev(apply(proof, unhash_keys_list(first(first(stackmap_get(pf, "used"))))),simp)
);

/*
 * Takes a proof, and proof steps list and returns the keys not used in the proof_steps.
 * Needed to create a "teacher's answer" in JSON block, including unused text.
*/
proof_parsons_keys_used_unused_hash(proof_ans, proof_steps) := block([tkeys, skeys],
  tkeys:map(first, hash_keys_array(proof_steps)),
  skeys:ev(proof_ans, map(lambda([ex], ex="["), proof_types), simp),
  /* TO-DO: update this when we deal with trees (and have examples) */
  skeys:map(lambda([skey], base64(skey)), flatten(skeys)),
  return([skeys, listdifference(tkeys, ev(unique(skeys), simp))])
);

/* Construct the "used" and "available" keys when the teacher's answer is used. */
parsons_answer(proof_ans, proof_steps) := block([pkeys],
  /* Ensure all keys are string keys. */
  if not(emptyp(proof_steps)) then proof_ans:proof_keys_sub(proof_ans, proof_steps),
  pkeys:proof_parsons_keys_used_unused_hash(proof_ans, proof_steps),
  sconcat("[[{\"used\":", stackjson_stringify([[first(pkeys)]]), ", \"available\":", stackjson_stringify(second(pkeys)), "}, 0]]")
);

/******************************************************************/
/*                                                                */
/*  Display functions                                             */
/*                                                                */
/******************************************************************/

/*
 * Hashes the first element of each sublist in a two-dimensional steps array using Base64 hashing. 
 *
 * It will turn `[["a", "b"], ["c", "d"], ["e", "f"]]` into `[["YQ==", "b"], ["Yg==", "d"], ["ZQ==", "f"]]`.
 */
hash_keys_array(steps) := block(
  return(map(lambda([item], join([base64(first(item))], rest(item, 1))), steps))
);

/*
 * Hashes all strings in a two-dimensional steps array using Base64 hashing. 
 *
 * It will turn `[["a", "b"], ["c", "d"], ["e", "f"]]` into `[["YQ==", "Yg=="], ["Yg==", "ZA=="], ["ZQ==", "Zg=="]]`.
 */
hash_2d_array(arr) := block(
    return(map(lambda([l], map(base64, l)), arr))
);

/*
 *  Return the step "k" from the proof "pf".
 */
proof_getstep(k, pf) := block([keylist],
    if integerp(k) then return(second(pf[k])),
    keylist:sublist(pf, lambda([ex], is(first(ex)=k))),
    if not(emptyp(keylist)) then return(second(first(keylist))),
    /* If the string is not in the pf list, then just return it unchanged.
       Teachers can use this to adapt proofs which use some of the steps.
    */
    k
);

/*
 * This function replaces integers and keys with the literal strings from the proof.
 */
proof_disp_replacesteps(ex, proof_steps) := block(
   if integerp(ex) or stringp(ex) then return(proof_getstep(ex, proof_steps)),
   if atom(ex) then return(sconcat("Error: the following atom does not index a step: ", string(ex))),
   /* Flatten any optional steps now. */
   if is(op(ex)=proof_opt) then return(proof_disp_replacesteps(first(args(ex)), proof_steps)),
   apply(op(ex), map(lambda([ex2], proof_disp_replacesteps(ex2, proof_steps)), args(ex)))
);

/**
 * Take a proof "ex" and a list "pf" of [key, step] pairs and translate this into a proof tree with
 * the keys replaced by corresponding strings.
 */
proof_keys_sub(ex, proof_steps):= block(
   if integerp(ex) then return(first(proof_steps[ex])),
   if stringp(ex) then return(ex),
   apply(op(ex), map(lambda([ex2], proof_keys_sub(ex2, proof_steps)), args(ex)))
);

/**
 * Take a proof "ex" and a list "pf" of [key, step] pairs and translate this into a proof tree with
 * the keys replaced by corresponding integers.
 */
proof_keys_int(ex, proof_steps):= block(
   if integerp(ex) then return(ex),
   if stringp(ex) then return(first(sublist_indices(proof_steps, lambda([ex2], is(ex=first(ex2)))))),
   apply(op(ex), map(lambda([ex2], proof_keys_int(ex2, proof_steps)), args(ex)))
);

/**
 * Replace displayed LaTeX mathematics delimiters with inline.
 */
proof_inline_maths(st) := ssubst("\\)", "\\]", ssubst("\\(", "\\[", st));

/*
 * Prune out any narrative from the proof steps: used to display a proof without narrative.
*/
proof_line_prune(pfs) := [first(pfs), second(pfs)];
proof_steps_prune(proof_steps) := map(proof_line_prune, proof_steps);

/*
 *  Return the step "k" from the proof "pf" wrapped in html divs, and with any narrative.
 */
proof_getstep_html(k, pf) := block([keylist],
    if integerp(k) then return(proof_line_html(pf[k])),
    keylist:sublist(pf, lambda([ex], is(first(ex)=k))),
    if not(emptyp(keylist)) then return(proof_line_html(first(keylist))),
    /* If the string is not in the pf list, then just return it unchanged.
       Teachers can use this to adapt proofs which use some of the steps.
    */
    k
);

/*
  Wrap lines in html <div> tags, and add narrative if it exists. 
  pfs is a line from the proof_steps array.
*/
proof_line_html(pfs) := block([st],
  st:"",
  if is(length(pfs)>2) then
      st:proof_comment_disp(third(pfs)),
  proof_line_disp(second(pfs), st)
);

/*
 * This function replaces integers and keys with the html-wrapped strings from the proof.
 */
proof_disp_replacesteps_html(ex, proof_steps) := block(
   if integerp(ex) or stringp(ex) then return(proof_getstep_html(ex, proof_steps)),
   if atom(ex) then return(sconcat("Error: the following atom does not index a step: ", string(ex))),
   /* Flatten any optional steps now. */
   if is(op(ex)=proof_opt) then return(proof_disp_replacesteps(first(args(ex)), proof_steps)),
   apply(op(ex), map(lambda([ex2], proof_disp_replacesteps_html(ex2, proof_steps)), args(ex)))
);

/**
 * Take a proof, and any proof steps and display them using proof CSS.
 */
proof_display(p_ans, p_steps) := block([p1],
    p1:proof_disp_replacesteps_html(p_ans, p_steps), 
    ev(p1,map(lambda([ex], ex=dispproof), proof_types))
);

/* Make use of the existing styles.  See https://docs.stack-assessment.org/en/Topics/Proof/ */
dispproof([ex]) := block([ex1],
    apply(sconcat, flatten(append(["<div class=\"proof-block\">"], [simplode(ex)], ["</div>"])))
);

/**
 * Take a proof, and any proof steps and display them using paragraphs.
 */
proof_display_para(proof_ans, proof_steps) := ev(proof_disp_replacesteps(proof_ans, proof_steps), map(lambda([ex], ex=dispproof_para), proof_types));

/* Flatten to a paragraph. */
dispproof_para([ex]) := block([ex1],
    apply(sconcat, flatten(append(["<p>"], [simplode(ex, " ")], ["</p>"])))
);

proof_line_disp(ex1, ex2):= sconcat("<div class='proof-line'>",    ex1, ex2, "</div>");
proof_comment_disp(ex):=    sconcat("<div class='proof-comment'>", ex,       "</div>");
proof_column_disp(ex):=     sconcat("<div class='proof-column'>",  ex,       "</div>");
proof_column_disp2(ex):=    sconcat("<div class='proof-column2'>", ex,       "</div>");

/***************************************************/
/* Display problems with proofs assesed by graphs. */
/***************************************************/

/* This function takes a list of problems, and the proof_steps, and displays feedback to the student. */
proof_dag_problem_display(saprob, poof_steps) := block(
    if not(listp(saprob)) then error("proof_dag_problem_display: first argument must be a list."),
    if emptyp(saprob) then return(""),
    if not(subsetp(setify(map(safe_op, saprob)),{"proof_step_must","proof_step_extra","proof_step_missing"})) then
        error("proof_dag_problem_display: first argument is not a list of proof_step_X functions."),
    /* Add in the proof_steps to the arguments of the functions. */
    saprob:map(lambda([ex], apply(op(ex), append(args(ex),[proof_steps]))), saprob),
    saprob:ev(saprob, proof_step_must=proof_step_must_disp,
                                  proof_step_extra=proof_step_extra_disp, 
                                  proof_step_missing=proof_step_missing_disp),
    /*
    saprob:simplode(saprob, "<hr/>"),
    */
    dispproof(simplode(saprob, "<hr/>"))
)$

/* Display functions for proof_step problem functions.
   One goal in choosing symbols is to avoid translation issues.
*/
proof_step_extra_disp(exs, ps):=block(
  simplode(map(lambda([ex], sconcat(proof_getstep(ex, ps), dl_delete_disp(true))), args(exs)), "<br/>")
)$
proof_step_missing_disp(exs, ps):=block(
  simplode(map(lambda([ex], sconcat("<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-left'></i> ? </span> ",
                                    proof_getstep(ex, ps))), args(exs)), "<br/>")
)$
proof_step_must_disp(ex1, ex2, ps):=block(
  sconcat(proof_getstep(ex1, ps),
          "<br/><span style='font-size: 1.1em; color:red;'>[?...?]</span><br/>",
          proof_getstep(ex2, ps),
          "<br/>")
)$

/******************************************************************/
/*                                                                */
/*  Matching functions                                            */
/*                                                                */
/******************************************************************/

/*
 * Auxiliary function.
 *
 * Takes a list of hashed answer keys and the full 2d steps array and returns the the all used hashed keys and unusued hashed keys.
 * Needed to create a "teacher's answer" in JSON format, including unused text.
 */
match_keys_used_unused_hash(ans, steps) := block([tkeys],
  tkeys:map(first, hash_keys_array(steps)),
  skeys:hash_2d_array(ans),
  return([skeys, listdifference(tkeys, ev(unique(flatten(skeys)), simp))])
);

/*
 * Use this to transform the teacher's answer into the shape expected by the Parson's block.
 * Returns an array of `[answer_keys, unused_keys]`, where `unused_keys` is always a flat
 * list of keys that are in the question but not inside `ans`.
 * 
 * If only `columns` has been specified in the `parsons` block, then use 
 * this function as `match_reshape_hash(ans1)`. This will return `answer_keys` as an 
 * array of shape `(columns, 1, ?)` if, where `?` represents variable dimension.
 * 
 * If both `rows` and `columns` have been specified in the `parson` block, then 
 * use this function as `match_reshape_hash(ans1, true)`. This will 
 * return `answer_keys` as an array of shape `(columns, rows, 1)`.
 */
match_reshape_hash(ans, steps, [rows]) := block([tkeys, akeys],
  tkeys: match_keys_used_unused_hash(ans, steps),
  if rows=[] then akeys: map(lambda([keys], [keys]), first(tkeys)) 
    else akeys:map(lambda([keys], map(lambda([k], [k]), keys)), first(tkeys)),
  return([akeys, second(tkeys)])
);

/*
 * Use this to transform the teacher's answer into the JSON format expected by the `Model answer` field.
 * 
 * If only `columns` has been specified in the `parsons` block, then use 
 * this function as `match_answer(ans1)`. 
 *
 * If both `rows` and `columns` have been specified in the `parson` block, then 
 * use this function as `match_answer(ans1, true)`. 
 */
match_answer(ans, steps, [rows]) := block([akeys, ukeys],
  if rows=[] then [akeys, ukeys]: match_reshape_hash(ans, steps)
    else [akeys, ukeys]: match_reshape_hash(ans, steps, rows),
  sconcat("[[{\"used\":", stackjson_stringify(akeys), ", \"available\":", stackjson_stringify(ukeys), "}, 0]]")
);

/*
 * Alias for for the column-only usage `match_answer(ans_steps)` of `match_answer`.
 */
group_answer(ans, steps) := match_answer(ans, steps);

/******************************************************************/
/*                                                                */
/*  Assessment and feedback functions                             */
/*                                                                */
/******************************************************************/

/* ********************************** */
/* Levenshtein distance               */
/* ********************************** */

/*
  Levenshtein distance with swap tracking
  s,t: lists to compare
  Returns integer d, the Levensthein distance between s and t.
  Returns the process of getting from s to t.
  Original author Achim Eichhorn Achim.Eichhorn(at)hs-esslingen.de modified by Chris Sangwin to track process.
*/
proof_damerau_levenstein(s, t) := block([c, m, n, XY, XYaction, i, j, d, temp, L, lm, li, dl_tags, simp],
    simp:true,
    if(s=t) then return([0,[]]), /* Equal strings result in 0, nothing to do. */
    m:length(s),
    n:length(t),
    XY: matrix(makelist(i,i,0,n), makelist(0,i,1,n+1)),
    XYaction: matrix(makelist(makelist(dl_add(t[k]),k,1,i),i,0,n), makelist([],i,1,n+1)),
    for i:1 thru m do (
        XY[2][1]:i,
        XYaction[2][1]:makelist(dl_delete(s[k]),k,1,i),
        for j:1 thru n do(
            c:if is(s[i]=t[j]) then 0 else 1,
            L:[XY[2][j]+1,     /* Insertion    */
                XY[1][j+1]+1,  /* Deletion     */
                XY[1][j]+c],   /* Substitution */
            /* Add in the swap rule. */
            /* The swapping costs nothing, but the cost comes from the subsequent dl_subs, which we filter out. */
            if is(i<m and j<n) and not(is(s[i]=s[i+1]))
                               and is(s[i]=t[j+1])
                               and is(s[i+1]=t[j]) then L:append(L,[XY[1][j]]),
            lm:apply(min,L),
            li:last(sublist_indices(L, lambda([ex], is(ex=lm)))),
            dl_tags:[append(XYaction[2][j],[dl_add(t[j])]),
                          append(XYaction[1][j+1],[dl_delete(s[i])]),
                          append(XYaction[1][j],[if is(c=0) then dl_ok(s[i]) else dl_subs(s[i],t[j])])],
            if is(i<m and j<n) then dl_tags:append(dl_tags,  [append(XYaction[1][j],[dl_swap(s[i],s[i+1])])]),
            XY[2][j+1]:L[li],
            XYaction[2][j+1]:dl_tags[li]
        ),
        XY:rowswap(XY, 1, 2),
        XYaction:rowswap(XYaction, 1, 2)
      ),
      return([XY[1][n+1],proof_damerau_levenstein_tidy(XYaction[1][n+1])])
);

/* dl_swap is always followed by a dl_subs, which we change. */
proof_damerau_levenstein_tidy(L) := block(
  if emptyp(L) then return(L),
  if op(first(L))=dl_swap then return(append([first(L),dl_swap_follow(second(first(L)))], proof_damerau_levenstein_tidy(rest(rest(L))))),
  return(append([first(L)], proof_damerau_levenstein_tidy(rest(L))))
);

/*
  This function performs assessment of the student's proof.
  sa is the student's proof
  ta is a list of possible proofs, typically tal:proof_alternatives(ta), but this is not enforced for flexibility.
     We assume the same keys are used in both.
  It returns a list.
  The fist item in the list is the "distance" from the student's proof in number of edits, using 
*/
proof_assessment(sa, tal) := block([sal, L, m, l],
  sal:args(proof_flatten(sa)),
  /* Lists are passed by reference, so let's copy for good measure since we flatten. */
  L:map(lambda([ex], args(proof_flatten(ex))), copy(tal)),
  /* Find distance from each proof, and the narrative */
  L:map(lambda([ex], proof_damerau_levenstein(sal, ex)), L),
  m:apply(min, map(first, L)),
  l:first(sublist_indices(L, lambda([ex], is(first(ex)=m)))),
  return(L[l]) 
 );

/* String values of the dl_functions used above. */
dl_empty_disp(ex) := "";
dl_add_disp(ex) := sconcat("<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-left'></i></span> ", ex);
dl_ok_disp(ex) := "<span style='font-size: 1.1em; color:green;'><i class='fa fa-check'></i></span>";
dl_delete_disp(ex) := "<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-right'></i></span>";
dl_swap_disp([ex]) := "<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-down'></i></span>";
dl_swap_follow_disp(ex) := "<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-up'></i></span>";
dl_subs_disp([ex]) := sconcat("<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-right'></i></span> ",
                            "<span style='font-size: 1.1em; color:red;'><i class='fa fa-arrow-left'></i></span> ", second(ex));

proof_line_disp(ex1, ex2):= sconcat("<div class='proof-line'>",    ex1, ex2, "</div>");
proof_comment_disp(ex):=    sconcat("<div class='proof-comment'>", ex,       "</div>");
proof_column_disp(ex):=     sconcat("<div class='proof-column'>",  ex,       "</div>");
proof_column_disp2(ex):=    sconcat("<div class='proof-column2'>", ex,       "</div>");

dl_disp(ex):=ev(ex, dl_empty=dl_empty_disp, dl_ok=dl_ok_disp, dl_delete=dl_delete_disp, dl_add=dl_add_disp,
                    dl_swap=dl_swap_disp, dl_swap_follow=dl_swap_follow_disp, dl_subs=dl_subs_disp);

proof_assessment_display(saa, pf) := block([st, k],
  /* An empty list is returned when we have a correct proof. */
  if emptyp(saa) then return(""),
  saa:proof_disp_replacesteps(saa, pf),
  /* sal is now a list of strings from the proof. */
  st:[],
  for k:1 thru length(saa) do block([s0,s1],
    s0:saa[k],
    s1:first(s0),
    if is(op(s0)=dl_add) then
        st:append(st, [[dl_empty(null), s0]])
    else
        st:append(st, [[s1, s0]])
  ),
  /* Turn the st list of lists into a string to display. */
    st:dl_disp(st),
  for k:1 thru length(saa) do block(
     st[k]:proof_line_disp(proof_column_disp(first(st[k])), proof_column_disp(second(st[k])))
  ),
  st:apply(sconcat, st),  
  sconcat("<div class='proof'>", st, "</div>")
);

/* ********************************** */
/* Bespoke graph                      */
/* ********************************** */

/*
For example, in this proof

proof_steps: [
    ["H1",     "This step is not needed in the proof."],
    ["S1",     "Assume that \\(3 \\cdot 2^{172} + 1\\) is a perfect square."],
    ["S2", "There is a positive integer \\(k\\) such that \\(3 \\cdot 2^{172} + 1 = k^2\\)."],
    ["S3",   "Since \\(3 \\cdot 2^{172} + 1 > 2^{172} = (2^{86})^2 > 172^2\\), we have \\(k > 172\\)."],
    ["S4",   "We have \\(k^2 = 3 \\cdot 2^{172} + 1 < 3 \\cdot 2^{172} + 173\\)."],
    ["S5",   "Also, \\(3 \\cdot 2^{172} + 173 = (3 \\cdot 2^{172} + 1) + 172 < k^2 + k\\). Further, \\(k^2 + k < (k + 1)^2\\)."],
    ["C6",    "Since \\(k^2 < 3 \\cdot 2^{172} + 173 < (k + 1)^2\\) it is strictly between two successive squares \\(k^2\\) and \\((k + 1)^2\\), it cannot be a perfect square."]
];

we have two possible "interleaved" answers.

sa1:["S1", "S2", "S3", "S4", "S5", "C6"];
sa2:["S1", "S2", "S3", "S5", "S4", "C6"];

These can be defined as a directed acyclic graph.

tdag: [
      ["S1", "S2", "S3", "S5", "C6"],
      ["S1", "S2", "S4", "C6"]
     ];
*/

/*
This function checks if the student's answer (sa, a list)
has the dependencies specified in the teachers graph (tdag).

It returns a list of "problems" in the form of 
 1. Missing steps.
 2. Unnecessary steps.
 3. Order problems.
*/
proof_assessment_dag(sa, tdag) := block([ttags,stags,proof_problems],
    ttags:setify(flatten(tdag)),
    if safe_op(sa)="proof" then sa:args(sa),
    stags:setify(flatten(sa)),
    proof_problems:[],
    /* Each key used in the teacher's proof must occur in the student's list. */
    if not(subsetp(stags, ttags)) then proof_problems:[proof_step_extra(setdifference(stags, ttags))],
    /* Only keys used in the teacher's proof should occur in the student's list. */
    if not(subsetp(ttags, stags)) then proof_problems:append(proof_problems,[proof_step_missing(setdifference(ttags, stags))]),
    /* For each list, we check that the keys occur in the specified order in the student's proof. */
    proof_problems:flatten(append(proof_problems, map(lambda([ex], proof_dag_check_list(sa, ex)), tdag))),
    /* Remove any duplicate problems. */
    listify(setify(proof_problems))
)$

/*
  Takes a list "l1", and makes sure sa respects the order of things in l1.
*/
proof_dag_check_list(sa, l1) := block([li1,li2,m1],
  if is(length(l1) < 2) then return([]),
  m1:[],
  /* By design we only check adjacent elements in the list. */
  li1:sublist_indices(sa, lambda([ex], ex=first(l1))),
  li2:sublist_indices(sa, lambda([ex], ex=second(l1))),
  /* Check if both steps appear. */
  if not(emptyp(li1) or emptyp(li2)) and apply(max, li1) > apply(min,li2) then m1:[proof_step_must(first(l1), second(l1))],
  append(m1, proof_dag_check_list(sa, rest(l1)))
)$
